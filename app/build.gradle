apply plugin: 'com.android.application'
apply plugin: 'com.mob.sdk'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}
android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    defaultConfig {
        applicationId rootProject.ext.android["applicationId"]
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME  : project.getName(),
                             "room.schemaLocation": "$projectDir/schemas".toString(),
                             "room.incremental"   : "true"]
            }
        }

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            //是否开启zip优化
            zipAlignEnabled true
            //是否开启混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "FrameDemo_${releaseTime()}.apk"
                }
            }
        }
    }

}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    kapt rootProject.ext.dependencies["room_compiler"]
    implementation project(":lib_common")
    implementation project(":lib_download")
    if (!isNeedUserCenterModule.toBoolean()) {
        implementation project(":module_usercenter")
    }
    if (!isNeedShareModule.toBoolean()) {
        implementation project(":module_share")
    }
    if (!isNeedMapModule.toBoolean()) {
        implementation project(":module_map")
    }
    if (!isNeedSearchModule.toBoolean()) {
        implementation project(":module_search")
    }
    if (!isNeedGankModule.toBoolean()) {
        implementation project(":module_gank")
    }
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    kapt rootProject.ext.dependencies["arouter-compiler"]


}
